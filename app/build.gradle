import org.apache.tools.ant.taskdefs.condition.Os

def getPlatformNdkBuildCommand() {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            instr -> properties.load(instr)
        }
        def ndkDir = properties.getProperty('ndk.dir')
        if (ndkDir == null) {
            throw new GradleException("The ndk.dir property in local.propeties is not set")
        }
        def ndkBuild = Os.isFamily(Os.FAMILY_WINDOWS) ? "$ndkDir/ndk-build.cmd" : "$ndkDir/ndk-build"
        return ndkBuild
    } else {
        throw new GradleException("The local.properties file does not exist")
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.pacoworks.hellorust"
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/jniLibs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        def ndkBuild = getPlatformNdkBuildCommand()
        commandLine "$ndkBuild", '-j8', '-C', file('src/main/jni').absolutePath
    }

    // Copy shared libs into jniLibs folder (Hacky workaround)
    task copySharedLibs(type: Copy) {
        from 'src/main/libs'
        into 'src/main/jniLibs'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    copySharedLibs.dependsOn ndkBuild
    copySharedLibs.execute()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
//    compile 'net.java.dev.jna:jna:4.1.0'
}
